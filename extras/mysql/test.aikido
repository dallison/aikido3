import mysql

function show_table (table) {
    if (table.size() == 0) {
        throw "No data"
    }
    var firstrow = table[0]
    var title = ""
    var colwidths = new int [firstrow.size()]

    var colno = 0
    var width = 0
    foreach col firstrow {
        colwidths[colno] = col.first.size() + 1
        width += colwidths[colno]
        colno++
    }

    foreach row table {
        colno = 0
        foreach col row {
            var v = col.second
            var w = v.size() + 1
            if (w > colwidths[colno]) {
                colwidths[colno] = w
            }
            colno++
        }
    }

    foreach col firstrow {
        title.append (col.first + " ")
    }
    println (title)
    foreach i width {
       '-' -> output
    }
    println ()
    foreach row table {
        var line = ""
        colno = 0
        foreach col row {
            var v = col.second
            var diff = colwidths[colno++] - v.size()
            line.append(v)
            if (diff > 0) {
                foreach i diff {
                    line.append (' ')
                }
            }
        }
        println (line)
    }
}

var db = new MySQL.Database ("localhost", "test", "test")

var stmt1 = db.prepare ("select personid,name,locationvalid from persons where personid=?")
stmt1.bind (1, 3)
var q = stmt1.execute()
show_table (q)

stmt1.bind (1, 1)
q = stmt1.execute()
show_table (q)

var stmt2 = db.prepare ("insert into persons (name) values (?)")
stmt2.bind (1, "foo")
stmt2.execute()



